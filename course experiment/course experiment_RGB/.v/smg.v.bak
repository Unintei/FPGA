module smg(
    input clk,
    input rst_n,
    input wire [4:0] ew_data,
    input wire [4:0] sn_data,
    output reg [3:0] seg_sel,
    output reg [6:0] seg_ment
);
//参数定义
parameter T2MS=100_000;//2ms
//中间信号定义
reg [17:0]cnt;
wire add_cnt;
wire end_cnt;
reg[4:0]        sel_data;//数码管显示用途 最大值9
//计数器2ms扫描一位数码管
always @(posedge clk or negedge rst_n)begin
    if(!rst_n)begin
        cnt <= 0;
    end
    else if(add_cnt)begin
        if(end_cnt)
            cnt <= 0;
        else
            cnt <= cnt + 1;
    end
end

assign add_cnt =1 ;
assign end_cnt = add_cnt && cnt==1*T2MS-1 ;
//
//数据选择器 低电平选中数码管
always  @(posedge clk or negedge rst_n)begin
    if(rst_n==1'b0)begin
        seg_sel<=4'b1110;//东西 南北
    end
    else if(end_cnt)begin//每2ms切换一位数码管
        seg_sel<={seg_sel[2:0],seg_sel[3]};//左移
    end     
end
//切换数码管显示
always  @(posedge clk or negedge rst_n)begin
    if(rst_n==1'b0)begin
        sel_data<=0;
    end
    else begin
        case(seg_sel)
            4'b1110:sel_data<=(sn_data>=10)?sn_data-10:sn_data;//南北灯个位
            4'b1101:sel_data<=(sn_data>=10)?1:0;//南北灯十位
            4'b1011:sel_data<=(ew_data>=10)?ew_data-10:ew_data;//东西灯个位
            4'b0111:sel_data<=(ew_data>=10)?1:0;//东西灯十位
            default:sel_data<=0;
        endcase
    end
end
//译码器
always  @(posedge clk or negedge rst_n)begin
    if(rst_n==1'b0)begin
         seg_ment<=7'h7f;//不亮
    end
    else begin
         case(sel_data)
            0:seg_ment<=7'h01;//0
            1:seg_ment<=7'h4f;//1
            2:seg_ment<=7'h12;//2
            3:seg_ment<=7'h06;//3
            4:seg_ment<=7'h4c;//4
            5:seg_ment<=7'h24;//5
            6:seg_ment<=7'h20;//6
            7:seg_ment<=7'h0f;//7
            8:seg_ment<=7'h00;//8
            9:seg_ment<=7'h04;//9
            default:seg_ment<=7'h7f;//不亮
         endcase
    end  
end 
endmodule
